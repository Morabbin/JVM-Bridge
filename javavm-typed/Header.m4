dnl JVM-Bridge -- bridge from FP languages and others to the Java VM
dnl Copyright (C) 2001 Ashley Yakeley <ashley@semantic.org>
dnl
dnl This library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU Lesser General Public
dnl License as published by the Free Software Foundation; either
dnl version 2.1 of the License, or (at your option) any later version.
dnl
dnl This library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl Lesser General Public License for more details.
dnl
dnl You should have received a copy of the GNU Lesser General Public
dnl License along with this library; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
dnl
dnl
define(`jvm_classname',`patsubst(`$1',`\.',`\/')')dnl
define(`name_short',`patsubst(patsubst(`$1',`^.*\.\([^.]+\)$',`\1'),`\$',`_')')dnl
define(`name_long',`patsubst(patsubst(`$1',`_',`__'),`[\.\$]',`_')')dnl
define(`first_J',`patsubst(`$1',`[A-Za-z0-9_.]+',`J`'\&')')dnl
define(`id_type_class',`first_J(name_long(`$1'))')dnl
define(`id_type_short',`first_J(name_short(`$1'))')dnl
define(`id_type_marker',`Class_`'id_type_class(`$1')')dnl
define(`id_type_marker_short',`Class_`'id_type_short(`$1')')dnl
define(`refclasses',`:java.lang.Class:java.lang.String:java.lang.Throwable:')dnl
dnl
define(`hs_type_def_new',`dnl
	data id_type_marker($1);
	instance IsJavaClassMarker id_type_marker($1) where
	{
		cName _ = "jvm_classname($1)";
	};
	type id_type_class($1) = ObjectRef id_type_marker($1);')dnl
dnl
define(`hs_type_def_ref',`dnl
	type id_type_marker($1) = id_type_marker_short($1);

	type id_type_class($1) = id_type_short($1);')dnl
dnl
define(`hs_type_def',`ifelse(index(refclasses,`:$1:'),`-1',hs_type_def_new(`$1'),hs_type_def_ref(`$1'))')dnl
dnl
define(`hs_class',`dnl

	-- $1
hs_type_def($1)')dnl
dnl
{-# LANGUAGE UndecidableInstances #-}
{-# OPTIONS -Wall -Werror #-}
-- This file was autogenerated from Header.m4.
module MODULENAME where
{
	import Foreign.JavaVM.Typed;
	
patsubst(include(CLASSLISTFILE),`^\(.+\)',`hs_class(\1)')
}
